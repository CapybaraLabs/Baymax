import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id "idea"
    id "java"
    id "com.gorylenko.gradle-git-properties" version "$gradleGitPluginVersion"
    id "com.github.ben-manes.versions" version "$versionsPluginVersion"
    id "org.springframework.boot" version "$springBootVersion"

    id "jacoco"
}

group = 'space.npstr.baymax'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

configurations {
    // fucks with spring boot jar, we don't need it anyways
    compile.exclude module: 'opus-java'
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    testRuntimeOnly platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

    implementation "net.dv8tion:JDA:$jdaVersion"
    implementation "ch.qos.logback:logback-classic"
    implementation "io.sentry:sentry-logback:$sentryVersion"
    implementation "io.sentry:sentry-spring-boot-4-starter:$sentryVersion"
    implementation "org.yaml:snakeyaml"
    implementation "com.github.ben-manes.caffeine:caffeine"
    implementation "com.vdurmont:emoji-java:$emojiVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "org.xerial:sqlite-jdbc"
    implementation "org.flywaydb:flyway-core"

    //spring
    implementation "org.springframework.boot:spring-boot-starter"

    //testing
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

dependencyUpdates.resolutionStrategy {
    componentSelection properReleasesOnly()
}

tasks.register("outdated") {
    dependsOn dependencyUpdates
}

dependencyLocking {
    lockAllConfigurations()
}

// ./gradlew resolveAndLockAll --write-locks
task resolveAndLockAll {
    doFirst {
        assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
        configurations.all {
            resolutionStrategy {
                componentSelection properReleasesOnly()
            }
        }
        configurations
                .findAll { it.canBeResolved }
                .each { it.resolve() }
    }
}

tasks.withType(JavaCompile) {
    dependsOn(clean, processResources)
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

bootRun {
    //compiling tests during bootRun increases the likelyhood of catching broken tests locally instead of on the CI
    dependsOn compileTestJava

    //pass in custom jvm args
    // source: https://stackoverflow.com/a/25079415
    // example: ./gradlew bootRun -PjvmArgs="--illegal-access=debug -Dwhatever=value"
    if (project.hasProperty('jvmArgs')) {
        //noinspection GroovyAssignabilityCheck
        jvmArgs project.jvmArgs.split('\\s+')
    }
}

bootJar {
    archiveFileName.set("baymax.jar")
    doLast {
        copy {
            from 'build/libs/baymax.jar'
            into '.'
        }
    }
}

test {
    useJUnitPlatform()
    jacoco {
        includes['space.npstr.baymax.*']
    }
}

processResources {
    //inject values into app.properties
    def projectVersion = project.version
    def projectGroup = project.group
    def projectName = project.name
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "project.version"   : projectVersion,
                "project.groupId"   : projectGroup,
                "project.artifactId": projectName,
                "env.BUILD_NUMBER"  : (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'DEV'),
                "env.BUILD_TIME"    : System.currentTimeMillis() + ''
        ]
    }
}

static def properReleasesOnly() {
    return { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = [
                    'alpha', 'beta', 'rc', 'm1', 'm2', 'm3', 'm4', 'm5', 'm6', 'preview',
            ].any {
                q -> selection.candidate.version.toLowerCase().contains(q) && !selection.candidate.module.equals("JDA")
            }
            if (rejected) {
                selection.reject('Not a release')
            }
        }
    }
}
